plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco-report-aggregation'
    id 'java'
    id 'com.adarshr.test-logger' version '4.0.0'
//    id 'jacoco'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'org.benchmarker'
version = rootProject.getVersion()

ext {
    resilience4jVersion = '2.2.0' // 사용할 버전을 여기에 기입하세요.
    set('springCloudVersion', "2023.0.1")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

testlogger {
    theme 'mocha' // project level
    slowThreshold 5000
}

// define exclude classes for Jacoco test coverage report
def excludeJacocoTestCoverageReport = [
        'org/benchmarker/bmcontroller/home/**',
        'org/benchmarker/bmcontroller/template/**',
        'org/benchmarker/bmcontroller/common/beans/**',
        'org/benchmarker/bmcontroller/user/controller/UserController.class',
        'org/benchmarker/BmControllerApplication.class',
        'org/benchmarker/bmcontroller/login/controller/LoginController.class',
        'org/benchmarker/bmcontroller/common/util/NoOp.class',
        'org/benchmarker/bmcontroller/controller/AdminUserController**',
        'org/benchmarker/bmcontroller/user/controller/GroupController.class',
        'org/benchmarker/bmcontroller/scheduler/**',
        'org/benchmarker/bmcontroller/scheduler/ScheduledTaskService**',
        'org/benchmarker/bmcontroller/agent/**',
        'org/benchmarker/bmagent/status/**',


]

jacoco {
    toolVersion = "0.8.8"
}

java {
    sourceCompatibility = '17'
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "io.jsonwebtoken:jjwt-api:0.11.1"

    testImplementation 'org.testng:testng:7.1.0'

    testImplementation 'io.projectreactor:reactor-test' // Verifier

    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.11.1"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.11.1"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation project(":bm-common")
    jacocoAggregation project(":bm-agent")
//    implementation "io.github.resilience4j:resilience4j-all:${resilience4jVersion}"
    implementation 'com.squareup.okhttp3:mockwebserver:4.9.1'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'


    implementation 'org.springframework.boot:spring-boot-starter-validation'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:postgresql:1.19.3'
    testImplementation "org.testcontainers:junit-jupiter:1.19.3"

    implementation 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    if (isAppleSilicon()) {
        runtimeOnly("io.netty:netty-resolver-dns-native-macos:4.1.94.Final:osx-aarch_64")
    }

}

boolean isAppleSilicon() {
    return System.getProperty("os.name") == "Mac OS X" && System.getProperty("os.arch") == "aarch64"
}

ext {
    snippetsDir = file("build/generated-snippets")
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()

    testlogger {
        theme 'standard'
        showExceptions true
        showStackTraces true
        showFullStackTraces false
        showCauses true
        slowThreshold 1000 // 1 초 이상 걸리는 테스트는 느린 테스트로 표시
        showSummary true
        showSimpleNames false
        showPassed true
        showSkipped true
        showFailed true
        showOnlySlow false
        showStandardStreams false
        showPassedStandardStreams true
        showSkippedStandardStreams true
        showFailedStandardStreams true
        logLevel 'lifecycle'
    }

    jacoco {
        excludes = ["org/benchmarker/bmcontroller/home/HomeController.class",
                    "org/benchmarker/bmcontroller/user/controller/UserController.class",
                    'org/benchmarker/bmcontroller/controller/AdminUserController**',
                    "org/benchmarker/bmcontroller/BmControllerApplication.class",
                    'org/benchmarker/bmcontroller/scheduler/ScheduledTaskService**',
                    'org/benchmarker/bmcontroller/agent/**']
    }
    finalizedBy jacocoTestReport
}

/**
 * REST Docs Setup
 */

tasks.register("restDocsTest", Test) {
    outputs.dir snippetsDir
    useJUnitPlatform {
        includeTags("restDocs")
    }

    finalizedBy "asciidoctor"
}

tasks.register("apiBuild", GradleBuild) {
    tasks = ["clean", "restDocsTest", "build"]
}

tasks.named("asciidoctor") {
    dependsOn restDocsTest
    dependsOn "test"

    inputs.dir snippetsDir
    configurations "asciidoctorExt"
    baseDirFollowsSourceDir() // 원본파일작업은 .adoc 디렉터리 기준
}

/**
 * Jacoco Test Coverage Setup
 */

jacocoTestReport {
    dependsOn (
            test
    )
    reports {
        csv.required = true
        xml.required = false
    }
    // Set the path to the source files
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: excludeJacocoTestCoverageReport)
    }))
    finalizedBy 'jacocoTestCoverageVerification'
}

// task for Jacoco test coverage verification
jacocoTestCoverageVerification {
    violationRules {
        rule {
            // Set the path to the source files
            classDirectories.setFrom(tasks.jacocoTestReport.classDirectories)
            element = 'CLASS'
            enabled = true

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
}
